// システム設定初期化
g1=640 g2=480	// 画面サイズを変数に設定
GSg1,g2;		// 画面サイズ変更
SM1;			// BGM1番を再生
GW33;			// 速度ウエイト

// キャラ描画関数
//IN: @ポインタにキャラ構造体の先頭を入れておくこと
#0{
	x=@ y=@(1					// 座標を取得
	a=CC@(2/2*3+@(3/3;			// 方向とアニメーション値を元に、画像番号の計算：CC拡張関数で計算
	@>3 @+1 b=@ b>8 b[@=0]		// アニメーションカウント処理
	a+@(1						// 画像の開始位置を足す
	Ga,x,y;						// 描画
}

// x,yを画面の範囲内に収める
#1{
	a=@ a<0 a[@=0]
	a=@ a+24 a>g1 a[@=g1 @-24]
	@>1
	a=@ a<0 a[@=0]
	a=@ a+32 a>g2 a[@=g2 @-32]
}

// 方向を踏まえて移動させる：x,y,hを使用
#2{
	a=CC(h+1)%8<3; a[y-8]		// UP
//	a=h a+1 a%8 a<3 a[y-8]		// UP：↑と同じ処理
	[a=h a-3 a%8 a<3 a[y+8]]	// DOWN
	a=h a-1 a%8 a<3 a[x+8]		// RIGHT
	[a=h a-5 a%8 a<3 a[x-8]]	// LEFT
}

// ランダム移動処理
#3{
	a=CR; a%100 a<40 a[.]

	// ランダム移動
	x=@ y=@(1
	a=CR; a%100 a<20
	a[
		h=CR; h%8	// ランダム方向
		#2			// 方向を踏まえて移動
		@=x @(1=y @(2=h	// SET
	][
		h=@(2
		#2			// 方向を踏まえて移動
		@=x @(1=y	// SET
	]
	#1		// 画面の範囲内に収める
}

// キャラデータの初期値設定用
//IN : @=先頭位置、a=画像番号
#4{
	@=CR; @%g1		// キャラX座標（ランダム位置を設定）
	@(1=CR; @(1%g2	// キャラY座標（ランダム位置を設定）
	@(2=CR; @(2%8 	// 方向（0=上、1=右上、・・・6=左、7=左上）
	@(3=0 			// アニメーション（(0-8)/3）
	@(4=a			// 絵の開始位置
}

// プレイヤーの周辺にはPOPさせない関数
//IN : @=先頭位置、a=画像番号 
#7{
	b7=c1 b8=c2	// プレイヤーの座標位置
	b7-150 b8-150
	a7=1 [
		x7=CR; x7%g1; y7=CR; y7%g2;
		a7=CHx7,y7,b7,b8,300,300;
	a7]
	@=x7;			// キャラX座標
	@(1=y7;			// キャラY座標
	@(2=CR; @(2%8 	// 方向（0=上、1=右上、・・・6=左、7=左上）
	@(3=0 			// アニメーション（(0-8)/3）
	@(4=a			// 絵の開始位置
}

// キャラの初期座標と、方向とアニメーションカウントを設定
// （1キャラ分を、構造体のように連続で変数5個使用）
@:c1 a=12 #4			// PLキャラの生成
@=CCg1/2; @(1=CCg2/3;	// PLキャラの初期位置

// 主なキーを押して離すまで待機
#5{
	S0;	// SE
	// 主なキーを離すまで待機
	[GF; k=0 k+KSp; k+KEn; k+KEs; k+KML; k+KMR; k];

	// 何か押すまで待機
	[
		GF;	// FLIP
		k=0 k+KSp; k+KEn; k+KEs; k+KML; k+KMR;
		k=CCk==0;	// 0なら1にする
	k];

	S1;	// SE
	// 主なキーを離すまで待機
	[GF; k=0 k+KSp; k+KEn; k+KEs; k+KML; k+KMR; k]
}

// メッセージウインドウを表示
//IN : e=文字列番号, x,y=表示位置
#6{
	b=CCe!=0&&e!=97; b[G10,x,y;] // ウインドウ描画
	x+20 y+16
	// オープニング
	b=CCe==0; b[
		GTx,y,"ANC-BC.1390年"; y+40;
		GTx,y,"アヴァリス帝国女帝のあなたは、突如無実の罪で民衆から詰め寄られた。"; y+40;
		GTx,y,"当初は民に無実を理解してもらおうとしたが、"; y+20;
		GTx,y,"その努力虚しく民衆は暴徒へと変化する。"; y+40;
		GTx,y,"急ぎ城から脱出したあなたを守るのはただ１人の信頼出来る側近の騎士。"; y+40;
		GTx,y,"あなたは騎士と共に隣国オーサスまで逃げ延びなければならないのであった。"; y+60;
		GTx,y,"- キーを押して始める -"; y+40;
		// ＝＝＝＝＝＝＝＝＝＝＝＝＝
		// シナリオ書きたいけど何故か7行超えて記載すると、#6を次呼び出した時にプログラムが落ちる…から一旦保留

	]
	
	// ゲームオーバー＆リトライ
	b=CCe==1; b[
		GTx,y,"- ??? -"; y+20;
		GTx,y,"死んでしまったね…。"; y+20;
		GTx,y,"アヴァリス帝国が滅亡すると困るから、時を戻してあげる。"; y+20;
		GTx,y,"　　- キーを押して時を戻す -";

		// ゲームオーバーのため初期化処理
		m1=0;	// 敵キャラをリセット
		e=0;		// イベント進行度（メッセージ）
		t=1; t1=1;	// タイム（フレーム） t0=0~100, t1=t0が100になったカウント(t1:Max999)
		q=0;	// qはスコア
		q1=0;	// q1は接触数
		q2=31;	// オーサスまでの距離
		q3=q2;	//（q3予約済み：オーサスまでの残り距離）
		q4=5;	// 騎士の初期耐久度
		q4+q7;	// 初期耐久度を死んでしまった分アップ
		q5=q4	// 騎士の残り耐久度
		
		q7+1;	// スコア倍率をカウントアップ（アップでスコア倍率減少）
		
		@:c1 a=12 #4			// PLキャラの生成
		@=CCg1/2; @(1=CCg2/3;	// PLキャラの初期位置
	]
	// 騎士殺しエンド
	b=CCe==2; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"あっ…騎士に当てちゃった…。";]
	b=CCe==3; b[GTx,y,"- 騎士 -"; y+20; GTx,y,"ぎゃああぁぁぁぁぁぁぁぁぁ";]
	b=CCe==4; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"騎士いいいいぃぃぃぃぃぃぃ"; e=1;]
	
	// オープニング会話
	b=CCe==5; b[y+20; GTx,y,"　アヴァリス帝国外　草原";]
	b=CCe==6; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"もうこれ以上歩けない…。";]
	b=CCe==7; b[GTx,y,"- 騎士 -"; y+20; GTx,y,"そんな事言っていられる状況ではありません！"; y+20; GTx,y,"お願いします、どうかオーサスまで頑張ってください！";]
	b=CCe==8; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"私の靴はあなたみたいに頑丈じゃないからもうボロボロよ…。"; y+20; GTx,y,"それにもう疲れたわ。私達はここまでよ…。";]
	b=CCe==9; b[GTx,y,"- 騎士 -"; y+20; GTx,y,"………。"; y+20; GTx,y,"分かりました。私があなたを背負ってオーサスまで走ります。";]
	b=CCe==10; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"無茶よ！オーサスまでどれだけあると思っているの！";y+20; GTx,y,"それにエネムド人の仕業かモンスターがそこかしこにいるわ！";]
	b=CCe==11; b[GTx,y,"- 騎士 -"; y+20; GTx,y,"だからこそです。このままでは疲れ果て、"; y+20; GTx,y,"モンスターに囲まれてしまいます。"; y+20; GTx,y,"なので私が走り、帝様はモンスターの対応をしてください。";]
	b=CCe==12; b[GTx,y,"- 騎士 -"; y+20; GTx,y,"唯一もちだせた帝国の宝具アダマスの鎌があります。"; y+20; GTx,y,"この鎌を操るぐらいなら背負われながらでも出来るはずです。"; y+20; GTx,y,"…いや。やって頂かなければなりません。";]
	b=CCe==13; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"わ、分かったわ。"; y+20; GTx,y,"けれど無理はしないでね。";]
	b=CCe==14; b[GTx,y,"- 騎士 -"; y+20; GTx,y,"もちろんです。帝様こそ無理をなさらず。"; y+20; GTx,y,"あ！間違っても私に鎌を当てないでくださいね。";]
	b=CCe==15; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"そんな事するわけないじゃない！";]
	b=CCe==16; b[GTx,y,"- 騎士 -"; y+20; GTx,y,"ですよね！！"; y+20; GTx,y,"モンスター相手なら数回は耐えられますが、"; y+20; GTx,y,"この鎌は斬れ味抜群なので、私でも耐えられません。";]
	b=CCe==17; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"…気をつけるわ。";]
	b=CCe==18; b[GTx,y,"- ??? -"; y+20; GTx,y,"戦闘が始まるよ！矢印キーで移動。"; y+20; GTx,y,"マウスで鎌を動かし、"; y+20; GTx,y,"敵を倒しながらオーサスまで逃げ延びてね！";]

	// エンディング会話
	b=CCe==19; b[SM2; GTx,y,"- 騎士 -"; y+20; GTx,y,"ぜぇ…ぜぇ…。"; y+20; GTx,y,"帝様、着きました…。オーサスです…。";]
	b=CCe==20; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"やっと辿りついたのね…。"; y+20; GTx,y,"ありがとう騎士。あなたは私の最高のナイトよ！";]
	b=CCe==21; b[GTx,y,"- 騎士 -"; y+20; GTx,y,"お褒めに、授かり、光栄です…。"; y+20; GTx,y,"ですが…私は…もう…。";]
	b=CCe==22; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"しっかりして！"; y+20; GTx,y,"嫌！こんな所で死なないで！";]
	b=CCe==23; b[GTx,y,"- 騎士 -"; y+20; GTx,y,"帝様…オーサスの王を…頼ってください…。"; y+20; GTx,y,"これを…。帝国の印綬です…。"; y+20; GTx,y,"帝様の証明が…出来ます…。";]
	b=CCe==24; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"そんな事よりあなたを宿に運ぶわ！"; y+20; GTx,y,"ほら、しっかり……重っ…。";]
	b=CCe==25; b[GTx,y,"- 騎士 -"; y+20; GTx,y,"ははは…。帝様では…運べませんよ…。"; y+20; GTx,y,"帝様に仕えれて…良かった…です………";]
	b=CCe==26; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"騎士！！";]
	b=CCe==27; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"くっ…死なせてたまるものですか…！";]
	b=CCe==28; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"アヴァリス帝国の王がアムに願い奉る…。"; y+20; GTx,y,"48からなるアムの一柱、フィルヴァレム…。";]
	b=CCe==29; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"私の最後の騎士なのです…。"; y+20; GTx,y,"どうか御神の名に近しいこの者を癒やしてください…。";]
	b=CCe==30; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"この身体のヴェリスを使って頂いて構いません…。"; y+20; GTx,y,"どうか…彼を………。";]
	b=CCe==31; b[GTx,y,"- 騎士 -"; y+20; GTx,y,"………";]
	b=CCe==32; b[GTx,y,"- 騎士 -"; y+20; GTx,y,"……………";]
	b=CCe==33; b[GTx,y,"- 騎士 -"; y+20; GTx,y,"…………うっ。";]
	b=CCe==34; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"騎士ー！！";]
	b=CCe==35; b[GTx,y,"- 騎士 -"; y+20; GTx,y,"あれ…。身体が…動く。";]
	b=CCe==36; b[GTx,y,"- 女帝 -"; y+20; GTx,y,"良かった…。"; y+20; GTx,y,"でも………疲れたわ…。あとはよろしく…zzZ.";]
	b=CCe==37; b[GTx,y,"- 騎士 -"; y+20; GTx,y,"えっ、帝様！大丈夫ですか！！";]
	b=CCe==38; b[GC; GT20,450,"Fix Score："; GT130,450,q6; e=96;]	// ダミー。エンディングに向けてのフェードアウト＆エンディング分岐

	// エンディング
	b=CCe==97; b[
		SM0;
		GTx,y,"こうしてアヴァリス帝国女帝のあなたはオーサスへ逃げ延びた。"; y+40;
		GTx,y,"その後、騎士は疲れ眠った女帝を抱えながらオーサス王に謁見。"; y+20;
		GTx,y,"事情を聞いたオーサス王は協力を申し出てくれた。"; y+80;
		GTx,y,"月日は流れ、ANC-BC.1400年。"; y+40;
		GTx,y,"帝国奪還に意気込む帝はオーサス王国の協力のもと、アヴァリス奪還作戦を始動。"; y+40;
		GTx,y,"アヴァリス帝国を取り戻すべく奮闘するのであった。"; y+60; 
		GTx,y,"その影には１人の騎士が常に必ず居ることは言うまでも無いだろう。"; e=98;
	]
	b=CCe==99; b[y+35 GTx,y,"「AVARISⅡ-The Return of the Empress-」につづく！！"; i2=0;]

	#5			// 主なキーを押して離すまで待機
}

e=0;		// イベント進行度（メッセージ）
t=1; t1=1; t2=1;	// タイム（フレーム） t0=0~100, t1=t0が100になったカウント(t1:Max999)
q=0;	// qはスコア
q1=0;	// q1は接触数
q2=31;	// オーサスまでの距離
q3=q2;	//（q3予約済み：オーサスまでの残り距離）
q4=5;	// 騎士の初期耐久度
q5=q4	// 騎士の残り耐久度
q6=0; q7=0;	// q6スコア、q7スコア倍率計算用
j=10;	// 同時出現最大数（難易度）
h1=46	// 鎌の当たり判定X軸
h2=42	// 鎌の当たり判定y軸
//-----------------------------------
#6; GC;	// オープニングイベント
// 背景画像を全画面に貼り付け
y=0 [x=0 [G1; x+16 a=x a<g1 a] y+16 a=y a<g2 a]
// キャラ描画
i=1 l=1 l+m1 [ @:c(i #0 i+5 l-1 l]
// ゲーム説明
e=5; i=14 [i-1
	x=78 y=338 e=CC18-i; #6;
i]
// LOOP
i2=1
[
	GC;					// 画面クリア

	// イベント処理
	b=CCq3<1; i=20 b[i-1
		x=78 y=338 e=CC38-i; #6;	// オーサス到着、エンディングイベント
	i]
	b=CCq5<1; i=2 b[i-1
		x=78 y=338 e=CC4-i; #6;		// 騎士の耐久度０イベント
	i]
	b=CCe==96; b[ GC; e=97 x=0 y=0 #6]	// エンディング
	b=CCe==98; b[e=99 x=78 y=338 #6]	// エンディング後の広告（AVARIS2）
	b=CCe==1; b[x=78 y=338 #6]		// ゲームオーバー、リトライイベント
	b=CCe==2; i=3 b[i-1
		x=78 y=338 e=CC4-i; #6;		// 騎士に鎌を当ててしまったイベント
	i]
	
	// キー判定
	f=0
	k=KLF; k[c1-8 c3=6 f=1]	// LEFT
	k=KRT; k[c1+8 c3=2 f=1]	// RIGHT
	k=KUP; k[c2-8 c3=0 f=1]	// UP
	k=KDW; k[c2+8 c3=4 f=1]	// DOWN
	// 動いていたら効果音（3歩に1回）
	f[
		f=c4 f%2 f[S54;]	// 効果音
		@:c1 #1		// 画面の範囲内に収める
	]

	// 全敵をプレイヤーに向けて移動
	n=6 i=m1 i[i-1
		@:c(n					// 敵キャラのアドレスを指定
		@(2=CL@,@(1,c1,c2,4;	// 方向を設定
		x=@ y=@(1 h=@(2
		#3
		n+5
	i]

	// 敵と当たり判定を行い、当たっていたら効果音を鳴らし、敵を消す。
	x=c1 y=c2 x+12 y+16	// PL座標の中心
	@:c6				// 敵の開始位置
	n=m1 n[n-1			// 敵数でループ
		a=CHx,y,@,@(1,24,32;	// 当たり判定
		a[
			p=m1 p*5; p+1;	// 末尾の敵の配列先頭番号
			@=c(p; p+1;
			@(1=c(p; p+1;
			@(2=c(p; p+1;
			@(3=c(p; p+1;
			@(4=c(p; p+1;
			m1-1;
			S2; n=0; q1+1;		// 効果音を鳴らしループ終了
		]
		@>5				// 次の敵へ
	n]

	// マウスカーソルとの当たり判定
	@:c1
	x=KMX; y=KMY; G116;
	n=m1 n+1 n[n-1
		a=CHx,y,@,@(1,h1,h2;	// 当たり判定
		a[
			// 主人公に当たっていたら終了イベント立ち上げ
			b=CCn==m1; b[
				e=2
			][
				// 指定の敵を削除し、詰める。
				p=m1 p*5; p+1;	// 末尾の敵の配列先頭番号
				// 置換
				@=c(p; p+1;
				@(1=c(p; p+1;
				@(2=c(p; p+1;
				@(3=c(p; p+1;
				@(4=c(p; p+1;
				m1-1;
			]
			S4; q+1; n=0;	// 効果音を鳴らしループ終了
		][	
			@>5				// 次のキャラへ
		]
	n]

	// 120フレームごと(119)に敵pop。pop数はレベル（t1）
	b=119 b<t b[
		@:c6 a=36 p=m1 p*5 @>p		// 敵データ作成のための初期値代入
		i=t1
		b1=j b1<i b1[i=j]
		i[i-1
			#7					// 敵を作成
			@>5 a+12			// NEXT
			b=a b>60 b[a=36]	// 絵の番号は36～60まで
			m1+1				// 敵の総数をカウントアップ
		i]
	]

	// 背景画像を全画面に貼り付け
	y=g2 y/2 y=CCy-g2; y+t2 [
		x=0 [G1; x+16 a=x a<g1 a]
	y+16 a=y a-t2 a<g2 a]


	// @を設定し、関数で全キャラを描画
	i=1 l=1 l+m1 [
		@:c(i #0
		i+5
	l-1 l]

	// UI背景
	GA50;
	r=100 g=200 b=150 x=10 y=10 GP;
	x+1 y+1 x1=200 y1=55 GB;
	x+x1 y+y1 GP;
	GA100;

	// UI文字
	q3=q2 q3-t1
	GT20,20,"オーサスまで残り："; GT175,20,q3;
	q5=q4 q5-q1
	GT20,40,"騎士の耐久度残り： "; GT175,40,q5;
	
	// スコア計算
	q6=q b6=4 b6-q7 b=CCb6>0;
	b[
		q6*b6
	][
		q6=q
	]
	GT20,450,"Score："; GT100,450,q6;
	
	// 敵出現カウントダウン
	GA50;
	r=100 g=200 b=150 x=g1 x-40 y=10 GP;
	x+1 y+1 x1=30 y1=33 GB;
	x+x1 y+y1 GP;
	GA100;
	b=t b%20 b<1 b[
		b1=5 b2=t b2/20 b1-b2
		b=CCb1<0; b[b1=0];
	]
	x-19
	GTx,20,b1;

	// マウスカーソルの座標を取得
	x=KMX; y=KMY; G116;

	// フレームカウント
	t+1 b=t b>120 b[t=0 t1+1]
	t2+1 b=g2 b/2 b<t2 b[t2=1]		// 背景動作用
	b=t1 b>999 b[t1=0]

	// 終了キー（Esc）判定
	k=KEs; k[i2=0]

	// 実際の画面に表示（FLIP）
	GF;
i2]	// i2が0以外ならループ
