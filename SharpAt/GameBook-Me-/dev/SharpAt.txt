g1=640 g2=480	// 画面サイズを変数に設定
GSg1,g2;		// 画面サイズ変更
GW33;			// 速度ウェイト

//-----------------------------------
// Loop
e=1
[
	//-------------------------------
	// - System -
	e1=1 			// タイトルループ
	e2=1			// メインシーンループ
	e3=1			// エンディングループ
	// - Title -
	d1=65 d2=50		// タイトル画像位置
	d3=191 d4=230	// 扉ノック画像位置
	d5=200 d6=340	// 立ち去る画像位置
	d7=100 d8=100	// 扉画像位置
	//-------------------------------
	// Title
	e1[
		// 残像処理（処理速度が低下する）
		GA30; GC; GA100;

		// 扉を開く縁取りを描画
		x2=KMX; y2=KMY;	// カーソル位置を取得
		a=CHx2,y2,d3,d4,250,75;
		a[
			GA30; a=2 // a:枠縁のサイズ
			r=50 g=100 b=50 x=d3 y=d4 x1=250 y1=75
			x-a y-a x1=CCx1+(a*2)+1; y1=CCy1+(a*2); GB; // a*2はスクエア
			GA100;
		]

		// 立ち去る縁取りを描画
		a=CHx2,y2,d5,d6,230,69;
		a[
			GA30; a=2 // a:枠縁のサイズ
			r=100 g=50 b=30 x=d5 y=d6 x1=230 y1=69
			x-a y-a x1=CCx1+(a*2)+1; y1=CCy1+(a*2); GB; // a*2はスクエア
			GA100;
		]
		
		// マウスクリック処理
		k=KML; k[
			// 扉をノック画像
			a=CHx2,y2,d3,d4,250,75; a[e1=0 S0;]
			// 立ち去る画像
			a=CHx2,y2,d5,d6,230,69; a[e=0 e1=0 e2=0 e3=0]
		]

		// タイトル表示
		G0,d1,d2;
		// 扉を開く
		G1,d3,d4;
		// 立ち去る
		G2,d5,d6;

		// バージョン表示
		GT580,450,"v1.00";

		// 終了キー（Esc）判定
		k=KEs; k[e=0 e1=0 e2=0 e3=0]
		
		// マウスカーソルの座標を取得
		x=KMX; y=KMY; G110;

		GF;
	e1]
	//-------------------------------
	
	//-------------------------------
	// Main Scen
	u=1 			// チャタリング制御（アンロック）
	c=0				// テキスト進行
	f=1				// BGMフラグ（鳴らしていれば立ち下げる）
	f1=1			// SEフラグ
	x2=10 y2=10		// テキスト表示位置基準
	p=40			// 行間
	l=11			// テキスト表示上限
	TL0; SM-1;
	e2[
		//GC;// 画面クリア
		GA10; GC; GA100;

		// キー判定
		b=0
		k=KEn;	k[b=1]
		k=KML;	k[b=1]
		b[
			u[
				u=0		// チャタリングロック		
				c+1		// テキストカウントアップ
			]
		][
			u=1		// チャタリングアンロック
		]

		// テキスト表示
		c1=c c1%l
		i=c1 i+1 i[i-1
			a=CC(c-i);			// ループ値降順→昇順へ
			t=a					// 表示テキスト確定

			a=CC(c1-i);			// テキスト高さ基準確定
			x3=x2
			y3=CCy2+(a*p);		// 次テキスト位置調整

			TGt,x3,y3;			// テキスト表示
		i]
		
		// 効果音やBGM処理(テキスト行数から-2する)
		b=CCc==21; b[S1;]	// ブザー音
		b=CCc==28; b[SM1;]	// BGM1
		b=CCc==33; b[SM-1;]	// BGM1
		b=CC(c>33)&&(f==1); b[SM2; f=0;]	// BGM2
		b=CC(c==89)&&(f1==1); b[S2; f1=0]	// 両替機音
		b=CCc==90; b[f1=1]	// フラグリセット
		b=CC(c==113)&&(f1==1); b[S3; f1=0]	// 少年チップ音
		b=CCc==114; b[f1=1]	// フラグリセット
		b=CC(c==117)&&(f1==1); b[S4; f1=0]	// 私チップ音
		b=CCc==118; b[f1=1]	// フラグリセット
		b=CC(c==119)&&(f1==1); b[S5; f1=0]	// ルーレット音
		b=CCc==192; b[e2=0]	// シナリオエンド
		
		// デバッグ表示
		z=10
		//GT570,z,"u ="; GT600,z,u; z+20
		//GT570,z,"c ="; GT600,z,c; z+20
		//GT570,z,"a ="; GT600,z,a; z+20
		//GT570,z,"t ="; GT600,z,t; z+20
		//GT570,z,"y3="; GT600,z,y3;z+20
		//GT570,z,"f ="; GT600,z,f; z+20

		// 終了キー（Esc）判定
		k=KEs; k[e=0 e1=0 e2=0 e3=0]

		GF;		// 実際の画面に表示（FLIP）

	e2]
	//-------------------------------

	//-------------------------------
	// Ending
	TL1; c=0 x3=0 y3=0	// 初期化
	f=0		// カウント用 
	f1=43	// 流れる速度
	e3[
		GA25; GC; GA100;
	
		// スタッフロールの流れるやつ
		f+1 b=f b>f1 b[f=0 c+1 f2=1]

		b=c b>79; b[e3=0]

		// テキスト表示
		a=c a*6
		i=a i+1 i[i-1
			a=CC(c-i);			// ループ値降順→昇順へ
			t=a					// 表示テキスト確定

			a=CC(c1-i);			// テキスト高さ基準確定
			x3=x2
			y3=CCy2+(a*p)+290;	// 次テキスト位置調整(オフセット有り)

			// 画面外のテキストは表示させない
			b=y3 b>0 b[TGt,x3,y3;]	// テキスト表示
		i]

		//デバック用
		//GT570,10,"f ="; GT600,10,f;
		//GT570,30,"c ="; GT600,30,c;

		// 終了キー（Esc）判定
		k=KEs; k[e=0 e1=0 e2=0 e3=0]

		GF;
	e3]

	// 終了処理
	SM-1;
	SM0;

e]